(**
 * (c) Copyright 2019 Bruno Bentzen. All rights reserved.
 * Released under Apache 2.0 license as described in the file LICENSE.
 * Desc: Performs file operations
 **)

open Basis

let read_file filename = 
  let lines = ref [] in
  let chan = open_in filename in
  try
    while true; do
      lines := input_line chan :: !lines
    done; !lines
  with End_of_file ->
    close_in chan;
    List.rev !lines ;;

let rec concat_string_list = function
  | [] -> ""
  | [s] -> s
  | s :: l -> s ^ "\n" ^ concat_string_list l

(* Parses a string *)

let parse_string s =
  let lb = Lexing.from_string s in
  Syntax.command Scanner.token lb

let token_list_of_string s =
  let lb = Lexing.from_string s in
  let rec helper l =
    try
      let t = Scanner.token lb in
      if t = Syntax.EOF then List.rev l else helper (t::l)
    with _ -> List.rev l in 
  helper []
  
let parse_file filename = 
  Debruijn.normalize_command (parse_string (concat_string_list (read_file filename)))

(* Handles directories *)

let parent dir =
  if dir = "" then "."
  else Filename.dirname dir

let normalize_path path =
  let parts = String.split_on_char '/' path in
  let rec loop acc = function
    | [] -> List.rev acc
    | "." :: rest -> loop acc rest
    | ".." :: rest ->
        begin
          match acc with
          | [] -> loop [".."] rest
          | _ :: rest_acc -> loop rest_acc rest
        end
    | part :: rest -> loop (part :: acc) rest
  in
  let normalized = loop [] parts in
  match normalized with
  | [] -> "."
  | parts -> String.concat "/" parts

let resolve_path current_file import_path =
  let candidate =
    if Filename.is_relative import_path then
      let parent_dir = parent current_file in
      if parent_dir = "." then import_path
      else Filename.concat parent_dir import_path
    else
      import_path
  in
  normalize_path candidate